{
id: "padlock",
zorder: "@include data/zorder.cfg:npcs",
zsub_order: -10,
prototype: ["standard_values"],


properties: {
	color: { type: "string", editor_info: { type: "enum", value:"'gold'", enum_values: ["gold", "silver", "green", "red", "purple", "blue", "cyan"] } },
	event_name: { type: "string", default: "", editor_info: { type: "text" } },

	set_to_animation: "def(string name) -> commands set(animation, { image: 'props/locks.png', duration: -1, id: name, rect: image_rect_for_color[color] })",
		
	image_rect_for_color: "{string->[int,int,int,int]} :: {
								'gold':			[0,0,15,15],
								'silver':		[16,0,31,15],
								'green':		[32,0,47,15],
								'red':			[48,0,63,15],
								'purple':		[64,0,79,15],
								'blue':			[80,0,95,15],
								'cyan':			[96,0,111,15]
							}",								

},
on_create: "bind_command(def() set_to_animation('normal'))",
on_load: "fire_event('create')",

on_start_level: "[ if(level.player.get_stored_event(event_name) = 1, remove_object(me)), animation('normal')]",
on_unlock: "[ level.player.store_event(event_name, 1),
				impact_cloud(mid_x,mid_y,'large') where impact_cloud = def(int new_x, int new_y, string size) -> commands [if(size = 'small', spawn('impact_cloud_small',new_x,new_y,1), spawn('impact_cloud',new_x,new_y,1)),play_sound__falloff('bump-2.wav')],
				map(range(1d2), spawn('bouncing_debris_chunk', midpoint_x + (10+1d5)*facing, midpoint_y - 1d10-5, {facing:facing, debris_type: enum metal_flat, init_vel: enum burst} )),
				map(range(1d2), spawn('bouncing_debris_chunk', midpoint_x + (10+1d5)*facing, midpoint_y - 1d10-5, {facing:facing, debris_type: enum metal_round, init_vel: enum burst} )),
				play_sound__falloff('blockbreak.ogg'),die()]",
no_compile_image: "props/locks.png",
editor_info: {
	category: "keys & triggers",
	help: "Place this on top of a door to lock it - the door will autodetect the lock visually
		sitting there, and become locked.  To make the game remember that the act of unlocking 
		has happened, you have to set a matching event name in both the lock and the key - once
		this event gets triggered, they will cease being there when you enter the level.",
},

on_end_anim: "set_to_animation('normal')",

animation: {
	image: "props/locks.png",
	id: "normal",
	rect: [0,0,15,15],

	#gold is the default
	duration: 1,
},
}
